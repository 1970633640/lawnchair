import org.gradle.api.tasks.wrapper.Wrapper.DistributionType

buildscript {
    ext.kotlin_version = '1.4.30'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath GRADLE_CLASS_PATH
        classpath PROTOBUF_CLASS_PATH
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    ext.addFrameworkJar = { String path ->
        def frameworkJar = new File(rootProject.projectDir, 'prebuilts/libs/' + path)
        if (!frameworkJar.exists()) {
            throw new IllegalArgumentException("Framework jar path doesn't exist")
        }
        gradle.projectsEvaluated {
            tasks.withType(JavaCompile) {
                options.bootstrapClasspath = files([frameworkJar] + (options.bootstrapClasspath.files as Iterable<File>))
            }
        }
    }
}

tasks {
    wrapper {
        gradleVersion = "5.5.1"
        distributionType = DistributionType.ALL
    }
}

//noinspection GroovyUnusedAssignment
final String SUPPORT_LIBS_VERSION = '28.0.0-alpha1'

Properties localProps = new Properties()
File localPropsFile = project.rootProject.file('local.properties')
if (localPropsFile.exists()) {
    localProps.load(localPropsFile.newDataInputStream())
}

def publicBranches = ["alpha", "beta", "q-pre-alpha"]
def ciBuild = localProps.getProperty("ciBuild") == "true" || System.getenv("CI_BUILD") == "true"
def ciBuildNumber = localProps.getProperty("buildNumber") ?: System.getenv("CI_BUILD_NUMBER")
def tagVersionName = System.getenv("TAG_VERSION_NAME")
def signed = ciBuild || localProps.getProperty("storePass") != null
//noinspection GroovyUnusedAssignment
def isPublicBuild = localProps.getProperty("public") == "true" || (ciBuild && publicBranches.contains(System.getenv("DRONE_BRANCH")))

final String FRAMEWORK_PREBUILTS_DIR = "prebuilts/"

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion COMPILE_SDK
    buildToolsVersion BUILD_TOOLS_VERSION

    signingConfigs {
        ci {
            storeFile file("debug.jks")
            storePassword localProps.getProperty("storePass") ?: System.getenv("STORE_PASSWORD")
            keyAlias "Lawnchair"
            keyPassword localProps.getProperty("keyPass") ?:System.getenv("KEY_PASSWORD")
        }
    }
    defaultConfig {
        minSdkVersion 25
        targetSdkVersion 30

        def majorMinor = "2.1"
        if (tagVersionName != null) {
            def major = Integer.valueOf(System.getenv("TAG_MAJOR"))
            def minor = Integer.valueOf(System.getenv("TAG_MINOR"))
            versionCode = major * 1000000 + minor * 10000 + Integer.valueOf(ciBuildNumber)
            versionName = tagVersionName
        } else if (ciBuild) {
            versionCode = 210000 + Integer.valueOf(ciBuildNumber)
            def drone = localProps.getProperty("public") == "true" || System.getenv("DRONE")
            versionName = majorMinor + (drone ? "-" : ".") + ciBuildNumber
        } else {
            versionCode 2
            versionName majorMinor
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "boolean", "FEATURE_QUINOA", "true"
        buildConfigField "boolean", "FEATURE_SETTINGS_SEARCH", "true"
        buildConfigField "boolean", "FEATURE_BUG_REPORTER", "true"

        def githubToken = localProps.getProperty('githubToken') ?: '"' + System.getenv("GITHUB_TOKEN") + '"'
        buildConfigField "String", "GITHUB_TOKEN", githubToken

        buildConfigField "boolean", "SIGNATURE_VERIFICATION", "false"

        def debugMenuCode = System.getenv("DEBUG_MENU_CODE") ?: "veryperu"
        buildConfigField "String", "DEBUG_MENU_CODE_HASH", '"' + debugMenuCode.digest('SHA-1') + '"'

        def accuWeatherKey = localProps.getProperty('accu_key') ?: '"' + System.getenv("ACCU_KEY") + '"'
        buildConfigField "String", "ACCUWEATHER_KEY", accuWeatherKey

        buildConfigField "String", "BRIDGE_DOWNLOAD_URL", "\"https://lawnchair.app/lawnfeed/?ref=lawnchair\""

        ndk {
            abiFilters "armeabi-v7a", "x86", "x86_64", "arm64-v8a"
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            if (signed) {
                if (System.getenv("CI_EVENT_TYPE") != "pull_request") {
                    signingConfig signingConfigs.ci
                }
            }

            dexOptions {
                matchingFallbacks = ['debug']
            }
        }

        optimized {
            initWith debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard.flags'
            if (signed) {
                if (System.getenv("CI_EVENT_TYPE") != "pull_request") {
                    signingConfig signingConfigs.ci
                }
            }
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard.flags'
            if (signed) {
                if (System.getenv("CI_EVENT_TYPE") != "pull_request") {
                    signingConfig signingConfigs.ci
                }
            }

            dexOptions {
                matchingFallbacks = ['release']
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // The flavor dimensions for build variants (e.g. lawnchairWithQuickstep, lawnchairWithoutQuickstep)
    // See: https://developer.android.com/studio/build/build-variants#flavor-dimensions
    flavorDimensions "app", "recents", "dist"

    productFlavors {
        lawnchair {
            dimension "app"
            applicationId 'ch.deletescape.lawnchair'
            testApplicationId 'com.android.launcher3.tests'
        }

        l3go {
            dimension "app"
            applicationId 'com.android.launcher3'
            testApplicationId 'com.android.launcher3.tests'
        }

        withQuickstep {
            dimension "recents"
        }

        withoutQuickstep {
            dimension "recents"
        }

        dev {
            dimension "dist"
            // Use v1 dev package name for compatibility testing
            if (localProps.getProperty("v1compat") != "true") {
                applicationIdSuffix ".dev"
            }
            versionNameSuffix "-dev"
        }

        ci {
            dimension "dist"
            applicationIdSuffix ".ci"
            if (tagVersionName == null) {
                versionNameSuffix "-ci-" + (localProps.getProperty("branch") ?: System.getenv("DRONE_BRANCH"))
            }

            buildConfigField "boolean", "SIGNATURE_VERIFICATION", "true"
        }

        plah {
            dimension "dist"
            applicationIdSuffix ".plah"

            buildConfigField "boolean", "SIGNATURE_VERIFICATION", "true"
            buildConfigField "boolean", "FEATURE_BUG_REPORTER", "false"
        }
    }

    android.variantFilter { variant ->
        // Hide all non "lawnchairWithQuickstep" tasks to make this a little more bearable
        if (!name.startsWith("lawnchairWithQuickstep")) {
            variant.setIgnore(true)
        }
    }

    sourceSets {
        main {
            res.srcDirs = ['res']
            java.srcDirs = ['src', 'src_plugins']
            aidl.srcDirs = ['src']
            assets.srcDirs = ['assets']
            manifest.srcFile 'AndroidManifest-common.xml'
            proto {
                srcDir 'protos/'
                srcDir 'proto_overrides/'
                srcDir 'proto_pixel/'
            }
        }

        debug {
            manifest.srcFile "AndroidManifest.xml"
        }

        optimized {
            manifest.srcFile "AndroidManifest.xml"
        }

        release {
            manifest.srcFile "AndroidManifest.xml"
        }

        androidTest {
            res.srcDirs = ['tests/res']
            java.srcDirs = ['tests/src', 'tests/tapl']
            manifest.srcFile "tests/AndroidManifest-common.xml"
        }

        androidTestDebug {
            manifest.srcFile "tests/AndroidManifest.xml"
        }

        lawnchair {
            res.srcDirs = ['lawnchair/res']
            java.srcDirs = ['src_flags', 'src_shortcuts_overrides', 'lawnchair/src']
            manifest.srcFile "lawnchair/AndroidManifest.xml"
//            aidl.srcDirs = ['lawnchair/aidl']
        }

//        lawnchairWithoutQuickstep {
//            manifest.srcFile "AndroidManifest.xml"
//        }
//
//        lawnchairWithQuickstep {
//            manifest.srcFile "quickstep/AndroidManifest-launcher.xml"
//        }

        l3go {
            res.srcDirs = ['go/res']
            java.srcDirs = ['go/src']
            manifest.srcFile "go/AndroidManifest.xml"
        }

//        l3goWithoutQuickstepDebug {
//            manifest.srcFile "AndroidManifest.xml"
//        }
//
//        l3goWithQuickstepDebug {
//            manifest.srcFile "quickstep/AndroidManifest-launcher.xml"
//        }

        withoutQuickstep {
            java.srcDirs = ['src_ui_overrides']
        }

        withQuickstep {
            res.srcDirs = ['quickstep/res', 'quickstep/recents_ui_overrides/res']
            java.srcDirs = ['quickstep/src', 'quickstep/recents_ui_overrides/src']
            manifest.srcFile "quickstep/AndroidManifest.xml"
        }

//        withQuickstepIconRecents {
//            res.srcDirs = ['quickstep/res', 'go/quickstep/res']
//            java.srcDirs = ['quickstep/src', 'go/quickstep/src']
//            manifest.srcFile "quickstep/AndroidManifest.xml"
//        }
    }

    addFrameworkJar('framework.jar')
}

allprojects {
    repositories {
        maven { url "../../../prebuilts/sdk/current/androidx/m2repository" }
        maven { url "../../../prebuilts/fullsdk-darwin/extras/android/m2repository" }
        maven { url "../../../prebuilts/fullsdk-linux/extras/android/m2repository" }
        mavenCentral()
        google()
    }
}

afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.resValue 'string', 'application_id', variant.applicationId
    }
}

dependencies {
    implementation "androidx.dynamicanimation:dynamicanimation:${ANDROID_X_VERSION}"
    implementation "androidx.cardview:cardview:${ANDROID_X_VERSION}"
    implementation "androidx.recyclerview:recyclerview:${ANDROID_X_VERSION}"
    implementation "androidx.preference:preference-ktx:${ANDROID_X_VERSION}"
    implementation "androidx.palette:palette-ktx:${ANDROID_X_VERSION}"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    //noinspection GradleDependency
    implementation "com.google.android.material:material:1.0.0"
    implementation project(':IconLoader')
    implementation project(path: ':SystemUISharedLib')
    withQuickstepImplementation project(':SharedLibWrapper')
    implementation fileTree(dir: "${FRAMEWORK_PREBUILTS_DIR}/libs", include: 'launcher_protos.jar')

    // Recents lib dependency
    withQuickstepImplementation project(':SystemUISharedLib')

    // Required for lawnchair to compile. This is already included in the sysui_shared.jar
    implementation fileTree(dir: "${FRAMEWORK_PREBUILTS_DIR}/libs", include: 'plugin_core.jar')

    implementation 'com.github.florent37:fiftyshadesof:1.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.github.KwabenBerko:OpenWeatherMap-Android-Library:2.0.1'
    implementation 'com.github.LawnchairLauncher:chroma:1.2.6'
    implementation 'com.github.LawnchairLauncher:hoko-lite:5f8466b'
    implementation 'me.jfenn:Attribouter:0.1.5'
    implementation 'com.github.bumptech.glide:glide:4.8.0'

    implementation 'com.github.topjohnwu.libsu:core:2.5.1'
    implementation 'eu.chainfire:librootjava:1.3.0'
    implementation 'com.luckycatlabs:SunriseSunsetCalculator:1.2'
    implementation 'me.mvdw.recyclerviewmergeadapter:recyclerviewmergeadapter:2.1.0'
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'

    implementation 'me.xdrop:fuzzywuzzy:1.2.0'

    lawnchairImplementation files('lawnchair/libs/sesame-lib.aar')

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation "org.mockito:mockito-core:1.9.5"
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation "androidx.annotation:annotation:${ANDROID_X_VERSION}"

    implementation 'com.google.protobuf:protobuf-lite:3.0.0'

    implementation 'com.github.ChickenHook:RestrictionBypass:2.2'
}

protobuf {
    // Configure the protoc executable
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
                javanano {
                    option "enum_style=java"
                }
            }
            task.plugins {
                javalite { }
            }
        }
    }
}

apply plugin: 'kotlin-android-extensions'
